group 'io.github.tjheslin1'
version '1.3'

apply plugin: 'java'

apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.assertj:assertj-core:3.5.2'
    testCompile "org.mockito:mockito-core:2.2.9"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task run(type: JavaExec) {
    main = "io.github.tjheslin1.tablewriter.Main"
    classpath = sourceSets.main.runtimeClasspath
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'TableWriter'
                packaging 'jar'
                description 'Fluent Java api for outputting a table of stats'
                url 'https://github.com/tjheslin1/TableWriter'

                scm {
                    url 'scm:git@github.com:TableWriter/TableWriter.git'
                    connection 'scm:git@github.com:TableWriter/TableWriter.git'
                    developerConnection 'scm:git@github.com:TableWriter/TableWriter.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tjheslin1'
                        name 'Thomas Heslin'
                    }
                }
            }
        }
    }
}